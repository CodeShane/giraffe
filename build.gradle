plugins {
    id "com.palantir.flexversion" version "0.9.2"
    id "com.palantir.jacoco-coverage" version "0.4.0" apply false
    id "com.github.hierynomus.license" version "0.13.1" apply false
}

apply from: 'gradle/versions.gradle'

addPrintVersionTask()
flexversion {
    envvarSources << 'CIRCLE_BRANCH'
    useTags = true
}

version flexVersion()
group = 'com.palantir.giraffe'

ext.localPublishDir = "${buildDir}/m2"

task distZip(type: Zip) {
    dependsOn (['core', 'ssh', 'fs-base', 'test-util'].collect { ":giraffe-${it}:publishJavaPublicationToLocalRepository" })

    from (localPublishDir) {
        exclude '**/maven-metadata.xml*'
    }

    baseName = project.name
    appendix = 'release'
    version = project.version

    destinationDir = file("${buildDir}/distributions")
    doFirst {
        file(destinationDir).mkdirs()
    }
}

task publish(dependsOn: distZip)

task clean(type: Delete) {
    delete buildDir
}
